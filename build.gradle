plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group 'io.github.amayaframework'
archivesBaseName = 'core-sun'
version = System.getenv('RELEASE_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    api group: 'org.atteo.classindex', name: 'classindex', version: '3.4'
    annotationProcessor group: 'org.atteo.classindex', name: 'classindex', version: '3.4'
    implementation group: 'cglib', name: 'cglib', version: '3.3.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.35'
    implementation group: 'io.github.amayaframework', name: 'filters', version: '1.0.7'
    api group: 'io.github.amayaframework', name: 'core-api', version: '1.0.3'
    implementation group: 'io.github.amayaframework', name: 'http-server', version: '1.0.4'
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId group
            artifactId archivesBaseName
            version version

            from components.java
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST
            description 'Amaya is a fairly lightweight web framework for Java, which guarantees speed, ' +
                    'ease of creating plugins/addons, flexibility and ease of use. Sun server implementation.'
            name 'core-sun'
            url 'https://github.com/AmayaFramework/amaya-core-sun'
            organization {
                name 'io.github.amayaframework'
                url 'https://github.com/AmayaFramework'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/AmayaFramework/amaya-core-sun/issues'
            }
            licenses {
                license {
                    name 'GNU General Public License v3.0'
                    url 'https://github.com/AmayaFramework/amaya-core-sun/blob/main/LICENSE'
                }
            }
            scm {
                url 'https://github.com/AmayaFramework/amaya-core-sun'
                connection 'scm:https://github.com/AmayaFramework/amaya-core-sun.git'
                developerConnection 'scm:https://github.com/AmayaFramework/amaya-core-sun.git'
            }
            developers {
                developer {
                    id 'dev'
                    name 'RomanQed'
                    email 'gbakaldin@gmail.com'
                }
            }
        }
    }
}