plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'io.github.amayaframework'
archivesBaseName = 'core-sun'
version = System.getenv('RELEASE_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/iogithubamayaframework-1157"
    }
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/iogithubamayaframework-1158"
    }
}

dependencies {
    api group: 'org.atteo.classindex', name: 'classindex', version: '3.4'
    api group: 'com.github.romanqed', name: 'java-utils', version: '1.0.0'
    api group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
    annotationProcessor group: 'org.atteo.classindex', name: 'classindex', version: '3.4'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.35'
    implementation group: 'io.github.amayaframework', name: 'http-server', version: '1.0.7'
    implementation group: 'io.github.amayaframework', name: 'filters', version: '1.0.8'
    implementation group: 'io.github.amayaframework', name: 'core-api', version: 'DEV-5'
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            groupId = group
            artifactId = archivesBaseName
            version = version
            from components.java
            // Configure pom
            pom {
                name.set(archivesBaseName)
                description.set('Amaya is a fairly lightweight web framework for Java, which guarantees speed, ' +
                        'ease of creating plugins/addons, flexibility and ease of use. Sun server implementation.')
                url.set('https://github.com/AmayaFramework/amaya-core-sun')
                organization {
                    name.set('io.github.amayaframework')
                    url.set('https://github.com/AmayaFramework')
                }
                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/AmayaFramework/amaya-core-sun/issues')
                }
                licenses {
                    license {
                        name.set('GNU General Public License v3.0')
                        url.set('https://github.com/AmayaFramework/amaya-core-sun/blob/main/LICENSE')
                    }
                }
                scm {
                    url.set('https://github.com/AmayaFramework/amaya-core-sun')
                    connection.set('scm:https://github.com/AmayaFramework/amaya-core-sun.git')
                    developerConnection.set('scm:https://github.com/AmayaFramework/amaya-core-sun.git')
                }
                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}